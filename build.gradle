plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.palantir.docker' version '0.34.0'
    id 'io.freefair.lombok' version '6.5.1'
}

group 'io.github.mfvanek'
version '1.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

application {
    mainClassName = 'io.github.mfvanek.spring5mvc.TracingSpringMvcDemoApp'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def springVersion = '5.3.24'
def jaegerVersion = '1.3.1'
def swaggerVersion = '3.0.0'

dependencies {
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:8.5.84'
    implementation "org.springframework:spring-webmvc:${springVersion}"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation "io.jaegertracing:jaeger-core:${jaegerVersion}"
    implementation "io.jaegertracing:jaeger-thrift:${jaegerVersion}"

    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'org.slf4j:slf4j-api:2.0.5'

    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    testImplementation enforcedPlatform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation "org.springframework:spring-test:${springVersion}"
    testImplementation 'com.jayway.jsonpath:json-path:2.7.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

test {
    useJUnitPlatform()
}

jar {
    classifier = 'default'
}

shadowJar {
    mergeServiceFiles()
    classifier = null
}

docker {
    name "${project.name}:${project.version}"
    dockerfile file('Dockerfile')
    files "$buildDir/libs/"
}

dockerPrepare.dependsOn jar, shadowJar
docker.dependsOn build
